<section>
  <h2 id='chapter-10'>10. Working with maps</h2>

  <p>
    In the last chapter we looked at some functions that we could use to work with lists. We looked at some functions from the <code>List</code> module, but we really spent most of the chapter on the <code>Enum</code> module. In this chapter, we'll spend some time talking about how to use <code>Enum.each/2</code> and <code>Enum.map/2</code> on maps instead of lists. Then we'll cover some functions from the <code>Map</code> module. You'll then see where you can find functions on your own, outside of this book in the big wide world.
  </p>

  <p>
    We spoke in that last chapter of using the <code>Enum.each/2</code> function to work with maps:
  </p>

  <blockquote>
    <p>
      Similar to this, we could <em>enumerate</em> through a map. If we were to take one of our maps from earlier...
    </p>

<pre><code>%{<span class='console-green'>"name"</span> => <span class='console-green'>"Roberto"</span>, <span class='console-green'>"age"</span> => <span class='console-green'>"30ish"</span>, <span class='console-green'>"gender"</span> => <span class='console-green'>"Male"</span>}</code></pre>

    <p>
      ...and write each key and value pair down, they may look something like this:
    </p>

    <dl>
      <dt><strong>Name</strong></dt><dd>Roberto</dd>
      <dt><strong>Age</strong></dt><dd>30ish</dd>
      <dt><strong>Gender</strong></dt><dd>Male</dd>
    </dl>
  </blockquote>

  <p>
    But we never got around to actually showing you how this was done! Well, enough dilly-dallying, here's an example:
  </p>

<pre><code>iex> person = %{"name" => "Roberto", "age" => "30ish", "gender" => "Male"}
%{<span class='console-green'>"name"</span> => <span class='console-green'>"Roberto"</span>, <span class='console-green'>"age"</span> => <span class='console-green'>"30ish"</span>, <span class='console-green'>"gender"</span> => <span class='console-green'>"Male"</span>}
iex> Enum.each(person, fn ({key, value}) -> IO.puts value end)
</code></pre>


  <p>
    Before I can go any further, Roberto interrupts. "Hey, those curly braces inside the function passed to <code>each</code> are weird! It looks like a map but it's not a map because it doesn't have the <code>%</code> before it. And it's not a list because the curliness of the brackets; a list has square brackets. So what is it?". Roberto is calling out this code specifically:
  </p>

<pre><code>fn ({key, value}) ...</code></pre>

  <p>
    The <code>{k, v}</code> here indicates a type of data in Elixir that we haven't seen before called a <em>tuple</em>. Tuples work like lists in that the order of the elements inside them matters, but you can't enumerate through them like you can with lists; said another way: tuples are <em>not</em> enumerable.
  </p>

  <p>
    In this function passed to <code>each</code>, a tuple is used instead of two function arguments (i.e. <code>fn(key, value) -></code>) to indicate that the <code>key</code> and the <code>value</code> share a connection. This <code>key</code> and <code>value</code> are not represented as a list (i.e. <code>fn([key, value]) -></code>) because of this connection. (Also: Elixir in its own "brain" thinks of maps as a list of tuples, and so this is another reason why they're tuples, and not lists!)
  </p>

  <aside>
    <header>More on tuples coming up real soon</header>
    <p>
      We'll see a lot more tuples when we get to <a href='/11-working-with-files'>Chapter 11 "Working with Files"</a>.
    </p>
  </aside>

  <p>
    Let's take another look at how we used <code>Enum.each/2</code> and what the output was:
  </p>

<pre><code>iex> Enum.each(person, fn ({key, value}) -> IO.puts value end)
30ish
Male
Roberto
:ok</code></pre>

  <p>
    Our use of <code>Enum.each/2</code> goes through each <code>key</code> and <code>value</code>, ignores the key and outputs the value with <code>IO.puts/1</code>. This is almost identical to our previous example of <code>Enum.each/2</code>, where we used a list of seasons with the <code>IO.puts/1</code> function:
  </p>

<pre><code>iex> Enum.each(seasons, &amp;IO.puts/1)</code></pre>

  <p>
    In the list example, we were able to use <code>&amp;IO.puts/1</code>. In
    the map example though, we had to pull out the value by using the tuple,
    and then we needed to use the expanded version of <code>IO.puts/1</code>
    (<code>IO.puts(value)</code>) to output that value. Let's look at what
    happens if we don't use that expanded version.
  </p>

<pre><code class='hljs text'>iex> Enum.each(person, &IO.puts/1)
<span class='console-red'>** (Protocol.UndefinedError) protocol String.Chars not implemented for {"age", "30ish"}</span></code></pre>

  <p>
    Uh oh, the computer is mad at us again. This cryptic error message is a complicated way of saying that <code>IO.puts/1</code> doesn't know how to output a tuple. We just need to be careful that we pass <code>IO.puts/1</code> string values only. If we give something that isn't a string to <code>IO.puts/1</code>, it won't know how to output it, and so it will show this error.
  </p>

  <p>
    We've now looked at <code>Enum.each/2</code> for maps, just like how we looked at it in the last chapter for lists. In that last chapter we also looked at <code>Enum.map/2</code> and <code>Enum.reduce/2</code> and so it's only fair that we cover them here too.
  </p>

</section>

<section>
  <h3>Mapping over maps</h3>

  <p>
    I wonder what map we could use this time. We've used the <code>person</code> map quite a few times already:
  </p>

<pre>
<code>iex> person = %{"name" => "Roberto", "age" => "30ish", "gender" => "Male"}</code></pre>

  <p>
    We should try to use another map for this section. Hmmmm. How about a map with the days of the week and their expected temperatures? Here's what the week ahead looks like in Melbourne at this current point of writing:
  </p>

<pre><code>%{
  "Monday" => 28,
  "Tuesday" => 29,
  "Wednesday" => 29,
  "Thursday" => 24,
  "Friday" => 16,
  "Saturday" => 16,
  "Sunday" => 20
}</code></pre>

  <p>
    These expected temperatures aren't just random numbers. That's just Melbourne for you. It just can't make up its mind. Now what if for our American friends we represented these numbers in their familiar &mdash; but wacky &mdash; farenheit? Otherwise they might read the numbers and think that Melbourne is a frigid hellscape, which <a href='https://www.theguardian.com/australia-news/2017/aug/16/melbourne-worlds-most-liveable-city-for-seventh-year-running'>it most certainly is not</a>.
  </p>

  <p>
    So we want to convert these temperatures from their celcius amounts into the farenheit equivalents, turning the map into this:
  </p>

<pre><code>%{
  "Monday" => 82.4,
  "Tuesday" => 84.2,
  "Wednesday" => 84.2,
  "Thursday" => 75.2,
  "Friday" => 60.8,
  "Saturday" => 60.8,
  "Sunday" => 68
}</code></pre>

  <p>
    We know already how to convert these numbers if it was simply list of those numbers, without the days of the week, like this:
  </p>

<pre><code>[28, 29, 29, 24, 16, 16, 20]</code></pre>

  <p>
    We would use <code>Enum.map/2</code> to run a function on each of these numbers to convert the numbers to farenheit (using that function that we saw back in <a href='/5-funky-functions'>Chapter 5</a>:
  </p>

<pre><code>iex> forecast = [28, 29, 29, 24, 16, 16, 20]
  [28, 29, 29, 24, 16, 16, 20]
  iex> Enum.map(forecast, fn (temp) -> temp * 1.8 + 32 end)
  [82.4, 84.2, 84.2, 75.2, 60.8, 60.8, 68.0]</code></pre>

  <p>
    So what if we just tried using <code>Enum.map/2</code> on our forecast map instead of on a list? What would that look like? Let's see:
  </p>

<pre><code>iex> forecast = %{
  "Monday" => 28,
  "Tuesday" => 29,
  "Wednesday" => 29,
  "Thursday" => 24,
  "Friday" => 16,
  "Saturday" => 16,
  "Sunday" => 20
}
%{"Friday" => 16, ...}
iex> Enum.map(forecast, fn ({day, temp}) -> {day, temp * 1.8 + 32} end)
[
  {"Friday", 60.8},
  {"Monday", 82.4},
  {"Saturday", 60.8},
  {"Sunday", 68.0},
  {"Thursday", 75.2},
  {"Tuesday", 84.2},
  {"Wednesday", 84.2}
]</code></pre>

  <p>
    "Hey, that's no map! That's a list!", Roberto exclaims, seemingly adapting a quote from a relatively niche space opera series. That's right, Roberto. When we call <code>Enum.map/2</code> on either a list or a map we're going to get back a list regardless. This has to do with how Elixir thinks of maps: it thinks of them as a list of key-value tuples, rather than the friendly <code>%{}</code> syntax that we know and love. To get this data back into the format that we're familiar with, we're going to need to use another function from the <code>Enum</code> toolbox, called <code>Enum.into/2</code>.
  </p>

  <p>
    Let's run our <code>Enum.map/2</code> line again, but this time we'll assign the output to a variable.
  </p>

<pre><code>iex> new_forecast = Enum.map(forecast, fn ({day, temp}) -> {day, temp * 1.8 + 32} end)
[
  {"Friday", 60.8},
  {"Monday", 82.4},
  {"Saturday", 60.8},
  {"Sunday", 68.0},
  {"Thursday", 75.2},
  {"Tuesday", 84.2},
  {"Wednesday", 84.2}
]</code></pre>

  <p>
    Then we can use <code>Enum.into/2</code> to convert this list of tuples back into a map:
  </p>

<pre><code>iex> Enum.into(new_forecast, %{})
%{"Friday" => 60.8, "Monday" => 82.4, "Saturday" => 60.8, "Sunday" => 68.0,
  "Thursday" => 75.2, "Tuesday" => 84.2, "Wednesday" => 84.2}</code></pre>

  <p>
    That's better! Our data is back into the shape of a map... but the days of the week have lost their ordering. That'll be a problem that I'll leave as an exercise for you to fix at the end of this chapter.
  </p>

  <p>
    So here we've seen how to use the <code>Enum.map/2</code> and <code>Enum.into/2</code> functions to iterate through a map's elements to run a function on each of those elements.
  </p>

  <p>
    However, there is a cleaner way of writing the above code and that's with the help of one of the best Elixir features: the <em>pipe operator</em>. Let's spend a little while looking at the pipe operator now.
  </p>
</section>

<section>
  <h3>The wonderful pipe operator</h3>

  <p>
    I've just shown you one way to use <code>Enum.map/2</code> to go over a map's values and to apply a function to each of those values. Here it is again, in case you missed it:
  </p>

<pre><code>
iex> forecast = %{
  "Monday" => 28,
  "Tuesday" => 29,
  "Wednesday" => 29,
  "Thursday" => 24,
  "Friday" => 16,
  "Saturday" => 16,
  "Sunday" => 20
}
%{"Friday" => 16, ...}
iex> new_forecast = Enum.map(forecast, fn ({day, temp}) -> {day, temp * 1.8 + 32} end)
  [
    {"Friday", 60.8},
    {"Monday", 82.4},
    {"Saturday", 60.8},
    {"Sunday", 68.0},
    {"Thursday", 75.2},
    {"Tuesday", 84.2},
    {"Wednesday", 84.2}
  ]
iex> Enum.into(new_forecast, %{})
%{
  "Friday" => 60.8,
  "Monday" => 82.4,
  "Saturday" => 60.8,
  "Sunday" => 68.0,
  "Thursday" => 75.2,
  "Tuesday" => 84.2,
  "Wednesday" => 84.2
}</code></pre>

  <p>
    What we're doing here can be broken down into a few steps:
  </p>

  <ol>
    <li>We define a variable called <code>forecast</code>, which is a map containing the days of the week and their expected temperatures.</li>
    <li>We then take this <code>forecast</code> variable, and pass it as the
      first argument to <code>Enum.map/2</code>. The second argument passed to
      <code>Enum.map/2</code> is a function which runs on each key-value pair
      in the map. This function takes the temperature -- the value -- and converts it to farenheit.</li>
    <li>We assign the result of this <code>Enum.map/2</code> call to a variable
      called <code>new_forecast</code>, and then immediately pass that into <code>Enum.into/2</code> as the first argument. The second argument given to <code>Enum.into/2</code> is an empty map. This converts the output of <code>Enum.map/2</code> -- a list of key-value tuples -- into a friendly map.</li>
  </ol>

  <p>
    Elixir has a way of writing this code in a shorter way that I would like to show you now as I believe it can lead to shorter, cleaner Elixir code. We might find more uses for it as we go along too, and so this seems like a pretty good time to introduce this particular concept.
  </p>

  <p>
    The way to write shorter code here is by using the <em>pipe operator</em>, <code>|&gt;</code>. The pipe operator allows us to chain together actions within Elixir, passing the result of one function on to the next. Unfortunately, unlike <a href='https://en.wikipedia.org/wiki/Tony_Hawk%27s_Pro_Skater'>Tony Hawk's Pro Skater</a>, we do not get thousands of points for chaining together sick combos. But we still do get the great sense of satisfaction when it all works together!
  </p>

  <p>
    Let's look at the actions we might take to get the forecast data and send it to our American friend:
  </p>

  <ul>
    <li>Get forecast data</li>
    <li>Then, convert it to farenheit</li>
    <li>Then, send it to our American friend</li>
  </ul>

  <p>
    We can't convert the data to farenheit unless we have it already, and we can't send it to our American friend -- or <em>shouldn't</em> -- before converting it to farenheit. We have to do these steps in order for them to make sense. If we were to write the above in Elixir code with the pipe operator, it might look like this:
  </p>

<pre><code>get_forecast_data
|> convert_to_farenheit
|> send_to_american_friend</code></pre>


  <p>
    Hey, that looks nice, doesn't it? You can think of the pipe operator as an
    "and then" operator. Do this action, <em>then</em> do this action <em>and
    then</em> do this final action. What would happen in our fictional code here is that the data from <code>get_forecast_data</code> would get passed to <code>convert_to_farenheit</code>, then <code>convert_to_farenheit</code> will pass its data onto the <code>send_to_american_friend</code> function.
  </p>

  <p>
    Rather than continuing to look at fictional code, let's switch back to a real example. Here's some code that uses the pipe operator ("and then") that is functionally identical to the code from the previous section, where we have a forecast in celcius and we convert it to farenheit:
  </p>

<pre><code>
%{
  "Monday" => 28,
  "Tuesday" => 29,
  "Wednesday" => 29,
  "Thursday" => 24,
  "Friday" => 16,
  "Saturday" => 16,
  "Sunday" => 20
}
|> Enum.map(fn ({day, temp}) -> {day, temp * 1.8 + 32} end)
|> Enum.into(%{})
|> IO.inspect
</code></pre>

  <p>
    Go ahead and put this code in a file called <code>forecast.exs</code> and
    run it with <code>elixir forecast.exs</code>. This is what you'll see as the output:
  </p>

<pre><code>%{
  "Friday" => 60.8,
  "Monday" => 82.4,
  "Saturday" => 60.8,
  "Sunday" => 68.0,
  "Thursday" => 75.2,
  "Tuesday" => 84.2,
  "Wednesday" => 84.2
}</code></pre>

  <p>
    I've put each key and value onto its own line, but you can see here that the output is the same as before in our <code>iex</code> console, and the code is neater to boot! "Amazing", utters Roberto. So what's happened here? Let's run through it:
  </p>

  <ol>
    <li>We define a map containing the days of the week and their expected temperatures. We <em>don't</em> assign this to a variable.</li>
    <li><em>And then</em> we <em>pipe</em> this map using the <em>pipe
        operator</em> (<code>|&gt;</code>) into <code>Enum.map/2</code>. This
      makes the map the first argument of <code>Enum.map/2</code>, so this
      removes the need to define a <code>forecast</code> variable and to pass
    it in as the first argument. The pipe operator gives the map to the
    function automatically. The only argument on <code>Enum.map/2</code> here
    is just the function that we use to convert the temperature values. We
    still refer to this function as <code>Enum.map/2</code>, because it is
    taking in two arguments: the piped-in value and the function.</l>
    <li><em>And then</em> we <em>pipe</em> the result of this
      <code>Enum.map/2</code> call into <code>Enum.into/2</code>, and the
      result of <code>Enum.map/2</code> becomes the first argument of
      <code>Enum.into/2</code>. The second argument to <code>Enum.into/2</code>
    is still an empty map.</li>
    <li>Finally, we <em>pipe</em> the result of <code>Enum.into/2</code> into a function we've not seen before called <code>IO.inspect/1</code>. This outputs the map in a human-friendly way. We're not using <code>IO.puts/1</code> here because it only works with strings, and what we have at this point of the code after <code>Enum.into/2</code> is a map, not a string. <code>IO.inspect/1</code> will work with any form of data that we give it.
  </ol>

  <p>
    "Wow, that's a lot of text!", says Roberto, looking a little stunned. "Do you have an image that I could look at instead?". Sure I do!
  </p>

  <figure>
    <img src='/10-maps/images/pipe-flow.png'>
    <figcaption>How data flows from one function to another</figcaption>
  </figure>

  <p>
    You can think of the pipe operator not only as an "and then" operator, but also like the stages of a waterfall -- or "datafall" -- with data flowing from one function to the next.

  <p>
    Each operation that we're doing in <code>forecast.exs</code> passes its result onto the next line, in a pipeline of operations. Elixir allows us to chain these operations together seamlessly with this pipe operator. Everything just flows from one line to the next until we reach the end. There is no need for storing data into temporary variables like <code>new_forecast</code>; you just simply pipe the output from one function to the next.
  </p>

  <p>
    So that's the wonderful pipe operator! How handy! We started this chapter talking about the <code>Enum</code> module's functions though, and we promised to talk about <code>each/2</code>, <code>map/2</code> and now we've done that. The next thing that was promised was to talk about some functions from the <code>Map</code> module. We might even find another excuse to use the pipe operator again.
  </p>
</section>

<section>
  <h3>The marvellous Map module and its fantastic contraptions</h3>

  <p>
    Rightio, so we've spent a lot of the last few pages looking at two modules: the <code>List</code> and <code>Enum</code> module. It's time for a breath of fresh air -- time to break out of our <code>Enum</code> comfort zone that we've wiggled into and time to get uncomfortable again! Let's look at a brand-new-to-us module: the <code>Map</code> module.
  </p>

  <p>
    If you want to do nearly anything with maps that doesn't involve <em>enumerating</em> over them, you would use this <code>Map</code> module. Let's look at a few common actions that you might take on a map.
  </p>

  <h4>Finding a key's value</h4>

  <p>
    In <a href='/4-marvellous-maps'>Chapter 4: Marvellous Maps</a> we saw that we could fetch the value of a particular key by using square brackets.
  </p>

<pre><code>iex> person = %{"gender" => "Male", "age" => "30ish", "name" => "Roberto"}
%{"age" => "30ish", "gender" => "Male", "name" => "Roberto"}
iex> person["name"]
"Roberto"</code></pre>

  <p>
    We compared it to a skilltester reaching in to pluck out the relevant item:
  </p>

  <figure>
    <img src='/images/4/skilltester.png' alt='skilltester'>
  </figure>

  <p>
    The <code>Map</code> module provides us another way to get that value out: <code>Map.get/2</code>.
  </p>

<pre><code>iex> Map.get(person, "name")</code></pre>

  <p>
    Now this might not seem very useful. After all, why not just use the old
    fashioned way: <code>person["name"]</code>? Well, one place where this
    might come in handy is if you want to find a particular key in the middle
    of a pipe chain. Imagine you have a function called
    <code>get_attendees</code> that gives you data that looks like this:
  </p>

<pre><code>%{
  "people" => ["Roberto", "The Author"],
  "robots" => ["Roboto", "TARS"]
}</code></pre>

  <p>
    Then from this data you wanted to get the first person from the list of
    people. You could write this as:
  </p>

  <pre><code>
  attendees = get_attendees()
  List.first(attendees["people"])
  </code></pre>

  <p>
    But then this code is confusing. The order looks like:
  </p>

  <ol>
    <li>Get list of attendees</li>
    <li>Get the first from that list...</li>
    <li>That list of <code>attendees["people"]</code></li>
  </ol>

  <p>
    Both Elixir and our brains need to interpret what's inside the brackets of
    <code>List.first/1</code> first, then interpret <code>List.first/1</code>.
    That requires us to jump around a little in the code because the code is
    not executed in the order that you would read it.
  </p>

  <p>
    If we were to use the pipe operator (<code>|></code>) and
    <code>Map.get/2</code>, we could really clean this code up and make it read
    a lot better. Here we go:
  </p>

  <pre><code>get_attendees() |> Map.get("people") |> List.first</code></pre>

  <p>
    That's much neater and it reads in the exact order of operations:
  </p>

  <ol>
    <li>Get the attendees</li>
    <li>Then, get the value that the <code>"people"</code> key points to</li>
    <li>Then, run <code>List.first/1</code> on that value</li>
  </ol>

  <p>
    So <code>Map.get/2</code> has its uses when pipe chaining. However, if you
    just wanted to access a value in a map regularly, I'd still say stick with
    code like <code>people["name"]</code>.
  </p>

  <h4>put</h4>

  <p>
    I want to show you one more function from <code>Map</code> which I think is
    as important to know as <code>Map.get/2</code> and that is
    <code>Map.put/2</code>. This function lets you update a map to add a key or
    update an existing key.
  </p>

  <p>
    You might want to use this function if you've got a map
    containing data that you want to update. For instance, like the
    data that we have on Roberto so far:
  </p>

<pre><code>iex> roberto = %{"name" => "Roberto", "age" => "30ish", "gender" => "Male"}</code></pre>

  <p>
    With this data, we're unclear about Roberto's age. Perhaps we've gotten to
    know him better throughout the course of this book and he tells us that
    he's actually 40ish instead of 30ish. This is surprising to us, considering his
    youthful visage. Nevertheless, our data is wrong. We need to update it. We
    could simply redefine the map:
  </p>

<pre><code>iex> roberto = %{<span class='unfocussed'>"name" => "Roberto"</span>, "age" => "40ish", <span class='unfocussed'>"gender" => "Male"</span>}</code></pre>

  <p>
  But that seems like an awful lot of typing just to update one key. Let's look at how we could update this key using <code>Map.put/2</code>:
  </p>

<pre><code>iex> Map.put(roberto, "age", "40ish")
%{"age" => "40ish", "gender" => "Male", "name" => "Roberto"}</code></pre>

<p>Ah that's so much better! We can now update the <code>"age"</code> key without having to rewrite the whole map.</code></p>








</section>


<section>
  <h3>Exercises</h3>

  <p>After mapping through our forecast map early in the chapter and converting its numbers from celcius to farenheit, we lost the proper ordering of the days of the week.</p>

<pre><code>%{"Friday" => 60.8, "Monday" => 82.4, "Saturday" => 60.8, "Sunday" => 68.0,
  "Thursday" => 75.2, "Tuesday" => 84.2, "Wednesday" => 84.2}</code></pre>

  <p>Can you find a function in <code>Enum</code> that would let us output the days of the week and their temperatures in the right order?</p>
</section>
